  {-# STDLIB_VERSION 6 #-}
  {-# CONTENT_TYPE DAPP #-}
  {-# SCRIPT_TYPE ACCOUNT #-}

  # Initialization status
  let k_initialized = "k_initialized"

  # Coordinator
  let k_coordinatorAddress = "k_coordinatorAddress"
  let k_admin_address = "k_admin_address"
  let k_nft_manager_address = "k_nft_manager_address"

  # Own constants  
  let k_rewarder_public_key = "k_rewarder_public_key"
  let k_spent = "k_spent"

  ##### Coordinator constants #####
  func coordinator() = addressFromString(this.getString(k_coordinatorAddress).valueOrErrorMessage("Coordinator not set")).valueOrErrorMessage("Coordinator address invalid")
  func adminAddress() = addressFromString(coordinator().getString(k_admin_address).valueOrErrorMessage("Admin address not set"))
  func rewarderPublicKey() = fromBase58String(this.getString(k_rewarder_public_key).valueOrErrorMessage("No rewarder public key set"))
  func nftManagerAddress() = addressFromString(coordinator().getString(k_nft_manager_address).valueOrErrorMessage("NFT Manager address not set")).valueOrErrorMessage("NFT Manager not set")

  func initialized() = this.getBoolean(k_initialized).valueOrElse(false)
  
  # Core constants
  let TYPE_ASSET = "asset"
  let TYPE_MINT = "mint"

  # Key functions
  func getSpentKey(_signature: String) = {
    k_spent + "," + _signature
  }

  func isSpent(_signature: String) = {
    let key = getSpentKey(_signature)
    this.getBoolean(key).valueOrElse(false)
  }

  @Callable(i)
  func initialize(
    _coordinator: String,
    _rewarderPublicKey: String
  ) = {
    if (initialized() || i.caller != this) then {
      throw("Unable to initialize")
    } else {
      [
        StringEntry(k_coordinatorAddress, addressFromStringValue(_coordinator).toString()),
        StringEntry(k_rewarder_public_key, _rewarderPublicKey),
        BooleanEntry(k_initialized, true)
      ]
    }
  }

  @Callable(i)
  func setRewarderPublicKey(
    _newRewarderPublicKey: String
  ) = {
    if (!initialized() || i.caller != adminAddress()) then {
      throw("Unable to initialize")
    } else {
      [
        StringEntry(k_rewarder_public_key, _newRewarderPublicKey)
      ]
    }
  }

  # This callable function allows a user to claim a prize given a nonce, recipient, 
  # asset ID, amount, and signature. It verifies the signature using the rewarder public key, 
  # checks that the caller is the recipient, and that the signature has not already been spent. 
  # It then splits the asset ID and amount into separate lists and creates a list of ScriptTransfers 
  # to transfer the assets to the recipient address. If the type is "mint", it invokes the NFT Manager 
  # contract to mint the asset to the recipient. Finally, it adds a BooleanEntry to the list indicating
  #  that the signature has been spent.
  @Callable(i)
  func claimPrize(
    _nonce: String,
    _recipient: String,
    _assetId: String,
    _amount: String,
    _signature: String
  ) = {
    let message = (
     _nonce
     + "," + _recipient
     + "," + _assetId
     + "," + _amount
    ).toBytes()

    if (
      !sigVerify(message, fromBase58String(_signature), rewarderPublicKey())
      || i.caller.toString() != _recipient
      || isSpent(_signature)
    ) then {
      throw("Invalid claimPrize parameters")
    } else {
      let assetIds = split(_assetId, ",")
      let amounts = split(_amount, ",")
      let recipientAddress = addressFromStringValue(_recipient)

      func zipAndTransfer(acc: (List[ScriptTransfer], Int), nextValue: String) = {
        let (result, index) = acc
        let valueParts = nextValue.split(":")
        let type = valueParts.getElement(0)
        let assetId = valueParts.getElement(1)
        let amount = parseIntValue(amounts.getElement(index))
        if (type == TYPE_ASSET) then {
          (
            result :+ ScriptTransfer(recipientAddress, amount, fromBase58String(assetId)),
            index + 1
          )
        } else {
          if (type == TYPE_MINT) then {
              strict invokeResult = invoke(
                    nftManagerAddress(), 
                    "mint",
                    [
                        _recipient,
                        assetId, 
                        amount
                    ], []
                )

              (
                result,
                index + 1
              )
          } else {
            throw("Invalid type: " + type)
          }
        }
      }
      let result = FOLD<10>(assetIds, ([], 0), zipAndTransfer)

      result._1
      ++
      [
        BooleanEntry(getSpentKey(_signature), true)
      ]
    }
  }

  @Verifier(tx)
  func verify() = {
    let coordinatorStr = this.getString(k_coordinatorAddress)
    if (coordinatorStr.isDefined()) then {
      let admin = addressFromStringValue(coordinatorStr.value()).getString(k_admin_address)
      if (admin.isDefined()) then {
        admin.value().addressFromStringValue().getBoolean("status_" + this.toString() + "_" + tx.id.toBase58String() ).valueOrElse(false)
      } else { 
        # Admin is not defined in coordinator, set admin first
        throw("unable to verify: admin not set in coordinator")
      }
    } else {
      # Coordinator not set, use native verify
      sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
    }
  }